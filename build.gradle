buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.1'
        classpath "io.spring.gradle:dependency-management-plugin:0.6.0.RELEASE"
        classpath 'com.netflix.nebula:gradle-extra-configurations-plugin:3.1.+'
    }
}


allprojects {
    group = 'ru.trylogic.spring.boot.thrift'
    version = '0.0.4.laximo'

    repositories {
        jcenter()
    }
    
    apply plugin: 'idea'
    apply plugin: "io.spring.dependency-management"
    apply plugin: 'nebula.provided-base'

    dependencyManagement {
        dependencies {
            dependencySet(group:'org.springframework.boot', version: '1.4.0.RELEASE') {
                entry "spring-boot-starter-web"
                entry "spring-boot-starter-test"
                entry "spring-boot-starter-actuator"
            }

            dependency "org.springframework:spring-core:4.3.2.RELEASE"

            dependency "org.apache.thrift:libthrift:0.9.3"

            dependency "org.projectlombok:lombok:1.16.10"
        }
    }
    
    task resolveAllDependencies {
        doLast {
            configurations.all { it.resolve() }
        }
    }
}

subprojects {
    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    test {
        reports {
            junitXml.enabled = true
            html.enabled = true
        }
    }
    
    dependencies {
        provided "org.projectlombok:lombok"

        testCompile "org.springframework.boot:spring-boot-starter-test"
    }
}